plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id "org.checkerframework" version "0.6.46" //https://github.com/kelloggm/checkerframework-gradle-plugin
}
if (project.hasProperty('ossrhUsername')) {
    apply from: 'publish.gradle'
}

group 'com.infomaximum'
version '0.1.23'

ext.moduleName = 'com.infomaximum.platform'

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
    }
}

compileJava {
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath
        ]
        classpath = files()
    }
}

sourceSets {
    main {
        java.srcDir 'src/main/java'
        resources.srcDir 'src/main/resources'
    }
    test {
        java.srcDir 'src/test/java'
        resources.srcDir 'src/test/resources'
    }
    test_integration {
        java.srcDir 'src/test_integration/java'
        resources.srcDir 'src/test_integration/resources'
        compileClasspath += sourceSets.main.output + sourceSets.test.output
        runtimeClasspath += sourceSets.main.output + sourceSets.test.output
    }
}

configurations {
    test_integrationImplementation.extendsFrom(testImplementation)
    test_integrationRuntimeOnly.extendsFrom(testRuntimeOnly)
}

repositories {
    mavenCentral()
}

dependencies {

    api 'com.infomaximum:cluster:1.1.11'
//    api project(':cluster')

    api 'com.infomaximum:cluster-graphql:1.1.14'
//    api project(':cluster-graphql')
    //Разобраться! Транзитивная зависимость,
    implementation('com.graphql-java:graphql-java:19.11') {
        exclude group: 'org.reactivestreams', module: 'reactive-streams'
        exclude group: 'com.graphql-java', module: 'java-dataloader'//from exclude org.slf4j
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }

    api 'com.infomaximum:rdao:1.14.15'
//    api project(':rdao')

    api 'com.infomaximum:network:1.2.8'
//    api project(':network')

    implementation 'io.prometheus:prometheus-metrics-core:1.3.5'
    implementation 'io.prometheus:prometheus-metrics-exporter-common:1.3.5'
    implementation ('io.prometheus:prometheus-metrics-instrumentation-jvm:1.3.5') {
        exclude group: 'io.prometheus', module: 'prometheus-metrics-core'
    }
    implementation (group: 'com.github.oshi', name: 'oshi-core', version: '6.6.5') {
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }

    implementation('org.reflections:reflections:0.10.2') {
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
        exclude group: 'org.javassist', module: 'javassist'
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }
    implementation 'org.javassist:javassist:3.30.2-GA'//Более новая зависимость с поддержкой java модулей

    implementation 'com.infomaximum:json-smart:2.5.1'

    implementation 'org.slf4j:slf4j-api:2.0.16'

    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.11.3'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.3'
    testImplementation 'org.assertj:assertj-core:3.24.2'
}

task test_integration(type: Test, description: 'Integration test', group: 'Verification') {}

tasks.withType(Copy).all { duplicatesStrategy 'exclude' }